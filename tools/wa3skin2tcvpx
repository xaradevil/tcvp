#! /usr/bin/perl
# -*- mmm-classes: here-doc -*-

# Copyright (C) 2003  Michael Ahlberg, Måns Rullgård
# Licensed under the Open Software License version 2.0

use strict;

use File::Temp qw/tempdir/;
use POSIX qw/floor/;
use File::Basename;
use Gimp qw/:auto/;
use XML::Parser;

die "Usage: wa3skin2tcvps skin dir\n" if $#ARGV != 1;

my $skin = $ARGV[0];
my $dir  = $ARGV[1];

my $sd;

-r $skin or die "Can't read $skin\n";

if(! -e $dir){
    mkdir($dir) or die "Can't create directory $dir.\n";
} elsif(-d $dir and ! -w $dir){
    die "Target directory $dir not writable.\n";
} elsif(! -d $dir){
    die "Target $dir exists and is non-directory.\n";
}

chomp(my $cwd = `pwd`);
$dir = "$cwd/$dir" unless $dir =~ m,^/,;

if(-d $skin){
    $sd = $skin;
} else {
    $sd = tempdir(CLEANUP => 1) or die "Can't create temp dir.\n";
    system "unzip -LL -qq -d $sd $skin" and die "Error unpacking skin.\n";
    system "chmod -R u+w $sd";
}

my $xml = new XML::Parser(Handlers => {Start => \&tag_start,
				       End => \&tag_end});
my $space;

my(%images, %groups, %layouts, $lc, $lg, $group, %imagefiles, $container);
my(%fonts);
my $xmldir = $sd;

sub max {
    $_[0] > $_[1]? $_[0]: $_[1];
}

# generic image helpers

sub save_image {
    my ($img, $f, $d) = @_;
    $f = "$dir/$f";
    $d = gimp_image_active_drawable($img) unless $d;
    gimp_file_save($img, $d, $f, $f);
}

sub load_image {
    my ($file) = @_;
    return $imagefiles{$file} if $imagefiles{$file};
    die "$file: not readable" unless -r $file;
    my $img = gimp_file_load($file, $file) or
      die "Can't load $file";
    gimp_drawable_is_rgb(gimp_image_active_drawable($img)) or
      gimp_convert_rgb($img);
    $imagefiles{$file} = $img;
    return $img;
}

sub make_rect {
    my ($w, $h, $x, $y, $ww, $hh, $bg, $name) = @_;
    my $nbg = gimp_image_new($w, $h, RGB);
    my $bl = gimp_layer_new($nbg, $w, $h, RGBA_IMAGE, 'foo', 0, NORMAL_MODE);
    gimp_image_add_layer($nbg, $bl, 0);
    gimp_layer_set_offsets($bl, 0, 0);
    gimp_edit_clear($bl);
    if($ww and $hh){
	gimp_rect_select($nbg, $x, $y, $ww, $hh, REPLACE, 0, 0);
	gimp_palette_set_background([@$bg[0..2]]);
	gimp_edit_fill($bl, BG_IMAGE_FILL);
    }
    save_image($nbg, $name, $bl);
    gimp_image_delete($nbg);
}

sub write_image {
    my ($id, $w, $h) = @_;
    if(not exists $images{$id}{img}){
	make_rect $w, $h, 0, 0, 0, 0, 0, "$id.png" if $w and $h;
	return;
    }
    my $im = $images{$id};
    if($h and ($w != $$im{width} or $h != $$im{height})){
	gimp_image_scale($$im{img}, $w, $h);
    }
    save_image $images{$id}{img}, "$id.png";
}

sub save_rect {
    my ($img, $x, $y, $w, $h, $out) = @_;
    my(@tl, @br, $b, $r, $s);
    gimp_rect_select($img, $x, $y, $w, $h, REPLACE, 0, 0);
    my $d = gimp_image_get_active_layer($img);
    gimp_layer_add_alpha($d);
    $r = $x + $w - 1;
    $b = $y + $h - 1;
    my $iw = gimp_image_width($img);
    my $ih = gimp_image_height($img);
    if($x + $w > $iw or $y + $h > $ih){
	my $nw = max $iw, $x + $w;
	my $nh = max $ih, $y + $h;
	gimp_image_resize($img, $nw, $nh, 0, 0);
	gimp_layer_resize($d, $nw, $nh, 0, 0);
    }
    @tl = gimp_drawable_get_pixel($d, $x, $y);
    gimp_drawable_set_pixel($d, $x, $y, 4, [255, 255, 255, 255]);
    eval { @br = gimp_drawable_get_pixel($d, $r, $b) };
    if(!$@){
	gimp_drawable_set_pixel($d, $r, $b, 4, [255, 255, 255, 255]);
	$s = 1;
    }
    gimp_edit_copy($d);
    $img = gimp_image_new($w, $h, RGB);
    my $tl = gimp_layer_new($img, $w, $h, RGBA_IMAGE, 'foo', 100, NORMAL_MODE);
    gimp_image_add_layer($img, $tl, -1);
    gimp_layer_set_offsets($tl, 0, 0);
    gimp_edit_clear($tl);
    my $l = gimp_edit_paste($tl, 0);
    gimp_layer_set_offsets($l, 0, 0);
    gimp_floating_sel_anchor($l);
    gimp_drawable_set_pixel($tl, 0, 0, 4, \@tl);
    gimp_drawable_set_pixel($d, $x, $y, 4, \@tl);
    if($s){
	gimp_drawable_set_pixel($tl, $w - 1, $h - 1, 4, \@br);
	gimp_drawable_set_pixel($d, $r, $b, 4, \@br);
    }
    $images{$out}{img} = $img;
    $images{$out}{width} = $w;
    $images{$out}{height} = $h;
}

# font utilities

sub text_properties {
    my ($img, $th) = @_;
    my ($i, $j, @c, $d, $md, @fg, @bg, $tt, $tb);
    my $w = gimp_image_width($img);
    my $h = gimp_image_height($img);
    my %cc;

    $d = gimp_image_active_drawable($img);

    $md = 0;
    $tt = -1;

    for($j = 0; $j < $h and $j < 3 * $th; $j++){
	my $rd = 0;

	for($i = 0; $i < $w; $i++){
	    @c = gimp_drawable_get_pixel($d, $i, $j);
	    $rd = $c[3] if $c[3] > $rd;
	    $cc{$c[0] + ($c[1] << 8) + ($c[2] << 16)}++ if $c[3] > 200;
	}
	if($rd > 50){
	    $tt = $j % $th if $tt < 0;
	    $tb = $j % $th;
	}
    }

    my $c;
    for(keys %cc){
	$c = $_ if $cc{$_} > $cc{$c};
    }
    @fg = ($c & 0xff, ($c >> 8) & 0xff, ($c >> 16) & 0xff);

    return (\@fg, $tt, $tb);
}

sub get_font {
    my ($f) = @_;
    return $fonts{$f} if exists $fonts{$f}{face};
    return undef unless exists $fonts{$f}{file};
    my $fnt = $fonts{$f};
    my $img = load_image $$fnt{file};
    my($color, $tt, $tb) = text_properties $img, $$fnt{charheight};
    my $th = $tb - $tt + 1;
    my $pxs = floor($th * 1.4);
    my $asp = $$fnt{charwidth} / ($$fnt{charheight} / 1.3);
    $$fnt{face} = "arial:pixelsize=$pxs:aspect=$asp:weight=bold";
    $$fnt{color} = sprintf "rgb:%02x/%02x/%02x", @$color;
    $$fnt{top} = $tt;
    $$fnt{bottom} = $tb;
    $$fnt{height} = $th;

    return $fnt;
}

sub tag_name {
    my($n) = @_;
    $n =~ s,[^[:alnum:]_/-],_,g;
    return $n;
}

my %actions = (prev => 'previous',
	       play => 'play',
	       pause => 'pause',
	       stop => 'stop',
	       next => 'next',
	       eject => 'open',
	       close => 'close_ui',
	       switch => sub { sprintf "replace_ui($container.%s.conf)", @_ });

my %display = (songname =>
	       { text => q/'${title:u} - ${performer:u}'/,
		 align => q/left/,
		 scroll_style => 2 },
	       time =>
	       { text => q/'${time} '/,
		 align => q/right/,
		 action => q/'toggle_time'/},
	       songinfo =>
	       { text => q/'${audio_bitrate} kbps  ${audio_samplerate} kHz'/,
		 scroll_style => 2 });

my %sliders = (seek =>
	       { action => q/'seek'/,
		 value => q/'${position}'/ },
	       volume =>
	       { action => q/'change_pcm_volume,set_variable(pcm_volume)'/,
		 value => q/'${pcm_volume}'/},
	       pan =>
	       { action => q/'change_balance,set_variable(balance)'/,
		 value => q/'${balance:-%f0.5}'/});

sub action {
    my($ac, $prm) = @_;
    my $act = $actions{lc $ac} or return $ac;
    if(defined &$act){
	return &$act($prm);
    } else {
	return sprintf $act, $prm;
    }
}

sub update_size {
    my($group, $x, $y, $w, $h) = @_;
    unless($$group{noresize}){
	$$group{width} = max($$group{width}, $x + $w);
	$$group{height} = max($$group{height}, $y + $h);
    }
}

sub tag_start {
    my(undef, $tag, %attr) = @_;
    return if $tag eq 'foo';
    $tag = lc $tag;
    print qq/$space$tag @{[map "$_=\"$attr{$_}\"", keys %attr]}\n/;
    $space .= '    ';
    map $attr{lc $_} = $attr{$_}, keys %attr;
    my($x, $y, $w, $h, $id) = @attr{'x', 'y', 'w', 'h', 'id'};
    my %wdg = (x => $x, 'y' => $y, width => $w, height => $h,
	       rx => $attr{relatx}, ry => $attr{relaty},
	       rw => $attr{relatw}, rh => $attr{relath});

    if($tag eq 'include'){			  # include
	my $xd = $xmldir;
	my $file = lc $attr{file};
	$xmldir = "$xmldir/" . dirname $file;
	open F, "$xmldir/" . basename $file;
	$xml->parse(join('', '<foo>', <F>, '</foo>'));
	close F;
	$xmldir = $xd;
    } elsif($tag eq 'groupdef'){		  # groupdef
	my $n = tag_name $id;
	$groups{$id}{id} = $id;
	$groups{$id}{groups} = [];
	$groups{$id}{widgets} = [];
	$lg = $groups{$id}{groups};
	$lc = $groups{$id}{widgets};
	$group = $groups{$id};
	$$group{width} = $w;
	$$group{height} = $h;
	$$group{text} = "$n [\n";
	$$group{name} = $n;
    } elsif($tag =~ /(toggle)?button/){		  # button
	my $img = $attr{image};
	return unless exists $images{$img};
	my $action = action $attr{action}, $attr{param};
	$wdg{width} = $images{$img}{width};
	$wdg{height} = $images{$img}{height};
	$wdg{text} = <<END_TCCONF;
button [
	action '$action'
	image '$img.png'
END_TCCONF
	write_image $img;
	if($attr{hoverimage}){
	    $wdg{text} .= "\tmouse_over '$attr{hoverimage}.png'\n";
	    write_image $attr{hoverimage};
	}
	if($attr{downimage}){
	    $wdg{text} .= "\tpressed '$attr{downimage}.png'\n";
	    write_image $attr{downimage};
	}
	push @$lc, \%wdg;
	update_size $group, $x, $y, $wdg{width}, $wdg{height};
    } elsif($tag eq 'bitmap'){			  # bitmap
	my $img = load_image "$sd/" . lc $attr{file};
	if(defined $x){
	    save_rect($img, $x, $y, $w, $h, $id);
	} else {
	    $images{$id}{img} = $img;
	    $images{$id}{width} = gimp_image_width($img);
	    $images{$id}{height} = gimp_image_height($img);
	}
    } elsif($tag eq 'layout'){			  # layout
	$id = "$container.$id";
	$layouts{$id}{id} = $id;
	$layouts{$id}{groups} = [];
	$layouts{$id}{widgets} = [];
	$lg = $layouts{$id}{groups};
	$lc = $layouts{$id}{widgets};
	$group = $layouts{$id};
	$$group{background} = $attr{background} || $id;
	my $tx;
	$tx .= "background '$$group{background}.png'\n";
	my $im = $images{$attr{background}};
	if($im){
	    $w = $$im{width};
	    $h = $$im{height};
	    $$group{noresize} = 1;
	} else {
	    $w or $w = $attr{minimum_w};
	    $h or $h = $attr{minimum_h};
	}
	$$group{width} = $w;
	$$group{height} = $h;
	$$group{text} = $tx;
    } elsif($tag eq 'group'){			  # group
	my $gn = tag_name $attr{id};
	$wdg{x} = 0 unless defined $x;
	$wdg{'y'} = 0 unless defined $y;
	push @$lg, $id;
	$wdg{text} = "box : $gn [\n";
	delete @wdg{'width', 'height'};
	push @$lc, \%wdg;
    } elsif($tag eq 'layer' and			  # layer
	    exists $attr{image}){
	my $im = $images{$attr{image}};
	$w or $w = $$im{width};
	$h or $h = $$im{height};
	update_size $group, $x, $y, $w, $h;
	$wdg{width} = $w;
	$wdg{height} = $h;
	$wdg{background} = $attr{image};
	$wdg{text} = <<END_TCCONF;
box [
	background '$attr{image}.png'
END_TCCONF
	push @$lc, \%wdg;
    } elsif($tag eq 'container'){		  # container
	$container = $attr{id};
    } elsif($tag eq 'bitmapfont'){		  # bitmapfont
	$fonts{$attr{id}}{file} = "$sd/" . lc $attr{file};
	$fonts{$attr{id}}{charwidth} = $attr{charwidth};
	$fonts{$attr{id}}{charheight} = $attr{charheight};
    } elsif($tag eq 'text'){			  # text
	my $font = get_font $attr{font} or
	  print "*** font $attr{font} not found\n" and return;
	my $d = $display{lc $attr{display}} or
	  print "*** display $attr{display} not found\n" and return;
	my $dh = $h - $$font{charheight} + $$font{top};
	$h = $$font{height};
	$y = $y + floor($dh / 2);
	$wdg{'y'} = $y;
	$wdg{height} = $h;
	$wdg{text} = <<END_TCCONF;
label [
	font '$$font{face}'
	color '$$font{color}'
END_TCCONF
	my $a;
	$wdg{text} .= "\talign '$a'\n" if $a = $attr{align} || $$d{align};
	map { $wdg{text} .= "\t$_ $$d{$_}\n" if exists $$d{$_} }
	  'text', 'action', 'scroll_style';
	update_size $group, $x, $y, $w, $h;
	push @$lc, \%wdg;
    } elsif($tag eq 'status'){			  # status
	my %wdg = (x => $x, 'y' => $y, width => $w, height => $h);
	$wdg{text} = <<END_TCCONF;
state [
	value '\${state:-stop}'
END_TCCONF
	for('stop', 'play', 'pause'){
	    my $im = $attr{$_ . 'bitmap'};
	    if($im){
		$wdg{text} .= "\timage '$_' '$im.png'\n";
		write_image $im;
	    }
	}
	update_size $group, $x, $y, $w, $h;
	push @$lc, \%wdg;
    } elsif($tag eq 'slider'){			  # slider
	return if $wdg{rw} or $wdg{rh} or $w <= 0 or $h <= 0;
	my($thumb, $ht, $dt) = @attr{'thumb', 'hoverthumb', 'downthumb'};
	my $dir = lc $attr{orientation} || 'horizontal';
	my($sx, $sy, $ex, $ey);
	my $th = $images{$thumb};
	if($dir eq 'horizontal'){
	    if($$th{height} > $h){
		$y += floor(($h - $$th{height}) / 2);
		$h = $$th{height};
	    }
	    $sx = floor($$th{width} / 2);
	    $ex = floor($w - $$th{width} / 2);
	    $sy = $ey = floor($h / 2);
	} elsif($dir eq 'vertical'){
	    if($$th{width} > $w){
		$x += floor(($w - $$th{width}) / 2);
		$w = $$th{width};
	    }
	    $sx = $ex = floor($w / 2);
	    $sy = floor($h - $$th{height} / 2);
	    $ey = floor($$th{height} / 2);
	} else {
	    return;
	}
	my %sl = (x => $x, 'y' => $y, width => $w, height => $h);
	$sl{text} = <<END_TCCONF;
slider [
	background '$$group{id}.$id.png'
	indicator '$thumb.png'
END_TCCONF
	make_rect $w, $h, 0, 0, 0, 0, 0, "$$group{id}.$id.png";
	write_image $thumb;
	if($ht){
	    $sl{text} .= "\tmouse_over '$ht.png'\n";
	    write_image $ht;
	}
	if($dt){
	    $sl{text} .= "\tpressed '$dt.png'\n";
	    write_image $dt;
	}
	$sl{text} .= <<END_TCCONF;
	start_position $sx $sy
	end_position $ex $ey
END_TCCONF
	my $sl = $sliders{lc $attr{action}};
	map { $sl{text} .= "\t$_ $$sl{$_}\n" if exists $$sl{$_} }
	  'value', 'action' if $sl;
	update_size $group, $x, $y, $w, $h;
	push @$lc, \%sl;
    }
}

sub tag_end {
    my(undef, $tag) = @_;
    return if $tag eq 'foo';
    $tag = lc $tag;
    $space = substr $space, 0, -4;
    if($tag eq 'groupdef'){
	undef $lc;
	undef $group;
    } elsif($tag eq 'layout'){
	undef $lc;
	undef $group;
    }	
}

sub print_group {
    my($fh, $gr) = @_;
    &print_group($fh, $groups{$_}) for(@{$$gr{groups}});
    my $tx = $$gr{text};
    my $wd;
    for $wd (@{$$gr{widgets}}){
	my($x, $y, $w, $h, $rx, $ry, $rw, $rh) = 
	  @$wd{'x', 'y', 'width', 'height', 'rx', 'ry', 'rw', 'rh'};
	$x += $$gr{width} if $rx;
	$y += $$gr{height} if $ry;
	$w += $$gr{width} - $x if $rw;
	$h += $$gr{height} - $y if $rh;
	$w = $$gr{width} - $x if($x + $w > $$gr{width});
	$h = $$gr{height} - $y if($y + $h > $$gr{height});
	$tx .= $$wd{text};
	$tx .= "	position $x $y\n" if defined $x;
	$tx .= "	size $w $h\n" if defined $w;
	if($$wd{background}){
	    write_image $$wd{background}, $w, $h;
	}
	$tx .= "]\n";
    }
    $tx .= "size $$gr{width} $$gr{height}\n";
    write_image $$gr{background}, $$gr{width}, $$gr{height};
    if($$gr{name}){
	$tx =~ s/(?<=\n)(?=.)/\t/sg;
	$tx .= "]\n";
    }
    print $fh $tx;
}

Gimp::on_net {
    $xml->parsefile("$sd/skin.xml");

    for(keys %layouts){
	open S, ">$dir/$_.conf" or die;
	print S "# -*- tcconf -*-\n";
	print_group \*S, $layouts{$_};
	close S;
    }
};

#Gimp::set_trace(TRACE_ALL);
exit main;
